managed; // implementation in class zbp_r_travel_0932 unique;
//strict ( 2 );     //Uncomment this line in order to enable strict mode 2. The strict mode has two variants (strict(1), strict(2)) and is prerequisite to be future proof regarding syntax and to be able to release your BO.

define behavior for ZR_TRAVEL_0932 alias Travel
implementation in class zbp_r_travel_0932 unique
persistent table ztb_travel_0932
with additional save    // llamar al class saver
lock master
authorization master ( instance )
etag master LastChangedAt
{
  create;
  update;
  delete;
  //field ( readonly ) TravelId;  // Default
  // Administrative fields
  field ( readonly ) LastChangedAt, LastChangedBy, CreatedAt, CreatedBy;
  // Mandatory fields
  field ( mandatory ) TravelId, AgencyId, BookingFee, CurrencyCode; // OverallStatus
  // Dynamic field control
  field ( features : instance ) TravelId, OverallStatus;
  // Validations
  validation validateCustomer on save { create; field CustomerId; }
  validation validateDates on save { create; field BeginDate, EndDate; }
  validation validateStatus on save { create; field OverallStatus; }
  // Actions
  action ( features : instance ) acceptTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;
  action createTravelByTemplate result [1] $self;
  // Mapping
  mapping for ztb_travel_0932
    {
      TravelId      = travel_id;
      AgencyId      = agency_id;
      CustomerId    = customer_id;
      BeginDate     = begin_date;
      EndDate       = end_date;
      BookingFee    = booking_fee;
      TotalPrice    = total_price;
      CurrencyCode  = currency_code;
      Description   = description;
      OverallStatus = overall_status;
      CreatedBy     = created_by;
      CreatedAt     = created_at;
      LastChangedBy = last_changed_by;
      LastChangedAt = last_changed_at;
    }

  //  association _Booking { create; }
  // En conjunto con la implementación GET_INSTANCE_FEATURES
  // Para la corrección de posibles bugs en la navegación
  association _Booking { create ( features : instance ); }
}

define behavior for ZR_BOOKING_0932 alias Booking
implementation in class zbp_r_booking_0932 unique
persistent table ztb_booking_0932
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt
{
  update;
  delete;
  //field ( readonly ) TravelId, BookingId;
  // Administrative fields
  field ( readonly ) TravelId;
  // Mandatory fields
  field ( mandatory ) BookingId, CarrierId, ConnectionId, FlightDate, BookingStatus;
  // Validations
  validation validateStatus on save { field BookingStatus; }
  // Determinations
  determination calculateTotalFlightPrice on modify { field FlightPrice, CurrencyCode; }
  determination calculateTotalSupplimPrice on modify { field CurrencyCode; }
  // Mapping
  mapping for ztb_booking_0932
    {
      TravelId      = travel_id;
      BookingId     = booking_id;
      BookingDate   = booking_date;
      CustomerId    = customer_id;
      CarrierId     = carrier_id;
      ConnectionId  = connection_id;
      FlightDate    = flight_date;
      FlightPrice   = flight_price;
      CurrencyCode  = currency_code;
      BookingStatus = booking_status;
      LastChangedAt = last_changed_at;
    }

  association _Travel;
  //  association _BookingSupplement { create; }
  // En conjunto con la implementación GET_INSTANCE_FEATURES
  // Para la corrección de posibles bugs en la navegación
  association _BookingSupplement { create ( features : instance ); }
}

define behavior for ZR_BOOKSUPPL_0932 alias BookingSupplement
implementation in class zbp_r_booksuppl_0932 unique
//persistent table ztb_booksup_0932
with unmanaged save
lock dependent by _Travel
authorization dependent by _Travel
etag master LastChangedAt
{
  update;
  delete;
  //field ( readonly ) TravelId, BookingId, BookingSupplementId;
  // Administrative fields
  field ( readonly ) TravelId, BookingId;
  // Mandatory fields
  field ( mandatory ) SupplementId, Price;
  // Determinations
  determination calculateTotalSupplimPrice on modify { field Price, CurrencyCode; }
  // Mapping
  mapping for ztb_log_0932
    {
      TravelId = travel_id;
    }

  association _Travel;
  association _Booking;
}